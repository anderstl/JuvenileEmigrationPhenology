
    model {
    
    # Priors and constraints
    for (i in 1:nind){
      for (t in f[i]:(n.occasions-1)){
        phi[i,t] <- (1/(1+exp(-(alpha[group[i]] + gamma[t]))))^int[t]     # Time and release cohort-dependent survival
        p[i,t] <- beta[m[i,t]]                                     # Constant recapture
      } #t
    } #i
    
    for(u in 1:2){
      beta[u] ~ dunif(0, 1)              # Priors for recapture
    }
    
    alpha[1] <- 0                        # Corner constraint
    alpha[2] ~ dnorm(0, 0.01)I(-10,10)   # Priors for difference in release cohort-spec. survival compared to treatment 1
    alpha[3] ~ dnorm(0, 0.01)I(-10,10)
    
    for(t in 1:(n.occasions-1)){
      gamma[t] ~ dnorm(0, 0.01)I(-10,10)       # Prior for time-dependent survival
      
      phi.1[t] <- 1/(1 + exp(-gamma[t]))             # Back-transformed survival of cohort 1
      phi.2[t] <- 1/(1 + exp(-gamma[t]-alpha[2]))    # Back-transformed survival of cohort 2
      phi.3[t] <- 1/(1 + exp(-gamma[t]-alpha[3]))    # Back-transformed survival of cohort 3
    }
    
    # Likelihood 
    for (i in 1:nind){
      # Define latent state at first capture 
      z[i,f[i]] <- 1
        for (t in (f[i]+1):n.occasions){
        # State process
          z[i,t] ~ dbern(mu1[i,t])
          mu1[i,t] <- phi[i,t-1] * z[i,t-1]
        # Observation process
          y[i,t] ~ dbern(mu2[i,t])
          mu2[i,t] <- p[i,t-1] * z[i,t]
        } #t
      } #i
    }
    
